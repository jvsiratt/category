category [ obj, arr: TYPE,
           dom, cod: [arr -> obj],
	   id: [obj -> arr],
	   o: [[f:arr, {g:arr | cod(g) = dom(f)}] -> arr] ]: THEORY
BEGIN

 ASSUMING

  id_dom_cod: ASSUMPTION
    FORALL (A:obj):
      dom(id(A)) = A AND cod(id(A)) = A

  comp_dom_cod: ASSUMPTION
    FORALL (f:arr, g:arr | cod(g) = dom(f)):
      dom(f o g) = dom(g) AND cod(f o g) = cod(f)

  unity: ASSUMPTION
    (FORALL (A:obj, f:arr | dom(f) = A):
       f o id(A) = f) AND
    (FORALL (A:obj, g:arr | cod(g) = A):
       id(A) o g = g)

  associativity: ASSUMPTION
    FORALL (h:arr, g:arr | cod(g) = dom(h), f:arr | cod(f) = dom(g)):
      h o (g o f) = (h o g) o f

 ENDASSUMING

  uniqueness_of_identity: THEOREM
   FORALL (A:obj, f:arr | dom(f) = A AND cod(f) = A):
    (FORALL (g:arr | dom(g) = A): g o f = g AND
     FORALL (g:arr | cod(g) = A): f o g = g) IMPLIES f = id(A)

  inverse?(f:arr, g:arr | dom(g) = cod(f) AND dom(f) = cod(g)): bool =
   f o g = id(dom(g)) AND g o f = id(dom(f))

  uniqueness_of_inverse: THEOREM
   FORALL (A,B:obj, f:arr | dom(f) = A AND cod(f) = B):
    FORALL (g,h:{x:arr | dom(x) = B AND cod(x) = A}):
     (inverse?(f,g) AND inverse?(f,h)) IMPLIES g = h

  iso?(f:arr): bool =
   EXISTS (g:arr | dom(g) = cod(f) AND dom(f) = cod(g)):
    f o g = id(cod(f)) AND g o f = id(dom(f))

  isomorphic?(A,B:obj): bool =
   EXISTS (f:arr | dom(f) = A AND cod(f) = B): iso?(f)

  isomorphic_reflexive: THEOREM
   FORALL (A:obj): isomorphic?(A,A)

  isomorphic_symmetry: THEOREM
   FORALL (A,B:obj): isomorphic?(A,B) IMPLIES isomorphic?(B,A)

  isomorphic_transitive: THEOREM
   FORALL (A,B,C:obj):
    (isomorphic?(A,B) AND isomorphic?(B,C)) IMPLIES isomorphic?(A,C)

  monic?(f:arr): bool =
   FORALL (A:obj, g,h:{x:arr | dom(x) = A AND cod(x) = dom(f)}):
    f o g = f o h IMPLIES g = h

  epic?(f:arr): bool =
   FORALL (A:obj, g,h:{x:arr | cod(x) = A AND dom(x) = cod(f)}):
    g o f = h o f IMPLIES g = h

  iso_monic_epic: THEOREM
   FORALL (f:arr):
    iso?(f) IMPLIES
     (monic?(f) AND epic?(f))

  initial?(A:obj): bool =
   FORALL (B:obj): EXISTS (f:arr | dom(f) = A AND cod(f) = B):
    FORALL (g:arr | dom(g) = A AND cod(g) = B): g = f

  teminal?(A:obj): bool =
   FORALL (B:obj): EXISTS (f:arr | dom(f) = B AND cod(f) = A):
    FORALL (g:arr | dom(g) = B AND cod(g) = A): g = f

END category
