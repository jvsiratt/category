category
 [ obj:TYPE,
   arr:TYPE,
   dom:[arr -> obj],
   cod:[arr -> obj],
   id:[obj -> arr],
   o:[[f:arr, {g:arr | cod(g) = dom(f)}] -> arr] ]: THEORY

 BEGIN

 ASSUMING

  id_dom_cod: ASSUMPTION
   FORALL (A:obj): dom(id(A)) = A AND cod(id(A)) = A

  comp_dom_cod: ASSUMPTION
   FORALL (f:arr,g:arr | cod(g) = dom(f)):
    dom(f o g) = dom(g) AND cod(f o g) = cod(f)

  unity_right: ASSUMPTION
   FORALL (A:obj, f:arr | dom(f) = A): f o id(A) = f

  unity_left: ASSUMPTION
   FORALL (A:obj, f:arr | cod(f) = A): id(A) o f = f

  associativity: ASSUMPTION
   FORALL (g:arr, f:arr | cod(f) = dom(g), h:arr | dom(h) = cod(g)):
    h o (g o f) = (h o g) o f

 ENDASSUMING

  uniqueness_of_identity: THEOREM
   FORALL (A:obj, f:arr | dom(f) = A AND cod(f) = A):
    ((FORALL (g:arr | dom(g) = A): g o f = g)
      AND FORALL (g:arr | cod(g) = A): f o g = g) IMPLIES
       f = id(A)

  inverse?(f:arr, g:arr | dom(g) = cod(f) AND dom(f) = cod(g)):bool =
   f o g = id(dom(g)) AND g o f = id(dom(f))

  uniqueness_of_inverse: THEOREM
   FORALL (A,B:obj, f:arr | dom(f) = A AND cod(f) = B):
    FORALL (g,h:{x:arr | dom(x) = B AND cod(x) = A}):
     (inverse?(f,g) AND inverse?(f,h)) IMPLIES
      g = h

  

 END category
