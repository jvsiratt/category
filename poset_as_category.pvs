poset_as_category [ ELEMENT: TYPE,
                     <=: [ELEMENT, ELEMENT -> bool] ]: THEORY
BEGIN

 ASSUMING

  reflexive: ASSUMPTION
    FORALL (x:ELEMENT):
      x <= x

  transitive: ASSUMPTION
    FORALL (x,y,z:ELEMENT):
      ( x <= y AND y <= z ) IMPLIES x <= z

  antisymmetric: ASSUMPTION
    FORALL (x,y:ELEMENT):
      ( x <= y AND y <= x) IMPLIES x = y

 ENDASSUMING

  obj: TYPE = ELEMENT
  arr: TYPE = [a:ELEMENT, {b:ELEMENT | a <= b}]
  dom(f:arr): obj = proj_1(f)
  cod(f:arr): obj = proj_2(f)
  id(A:obj): arr = (A, A)
  compose(f:arr, g:arr | cod(g) = dom(f)): arr = (dom(g), cod(f))

  IMPORTING category[obj, arr, dom, cod, id, compose]

END poset_as_category
