product_category [ obj1, arr1: TYPE,
                   dom1, cod1: [arr1 -> obj1],
		   id1: [obj1 -> arr1],
		   comp1: [[f:arr1, {g:arr1 | cod1(g) = dom1(f)}] -> arr1],
		   obj2, arr2: TYPE,
                   dom2, cod2: [arr2 -> obj2],
		   id2: [obj2 -> arr2],
		   comp2: [[f:arr2, {g:arr2 | cod2(g) = dom2(f)}] -> arr2]
		 ]: THEORY

BEGIN

 ASSUMING IMPORTING category_assumptions[obj1,arr1], category_assumptions[obj2,arr2]

  one_is_category: ASSUMPTION
    is_category?[obj1,arr1] (dom1,cod1,id1,comp1)

  two_is_category: ASSUMPTION
    is_category?[obj2,arr2] (dom2,cod2,id2,comp2)

 ENDASSUMING

 obj : TYPE = [obj1,obj2]
 arr : TYPE = [arr1,arr2]
 dom : [arr -> obj] = LAMBDA (f:arr) : (dom1(proj_1(f)),dom2(proj_2(f)))
 cod : [arr -> obj] = LAMBDA (f:arr) : (cod1(proj_1(f)),cod2(proj_2(f)))
 id : [obj -> arr] = LAMBDA (A:obj) : (id1(proj_1(A)),id2(proj_2(A)))
 comp : [[f:arr, {g:arr | cod(g) = dom(f)}] -> arr] =
   LAMBDA (f:arr, g:arr | cod(g) = dom(f)) :
     (comp1(proj_1(f),proj_1(g)),comp2(proj_2(f),proj_2(g)))

 IMPORTING category_assumptions[obj,arr]

 this_is_category: THEOREM
   is_category? (dom,cod,id,comp)

END product_category
